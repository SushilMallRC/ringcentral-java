plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
    id 'jacoco'
}

group 'app.nirmal'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}
dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.11.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'test-ringcentral'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Test RingCentral SDK'
                description = 'Test RingCentral Java SDK'
                url = 'https://github.com/SushilMallRC/ringcentral-java'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'SushilMallRC'
                        name = 'Sushil Mall'
                        email = 'sushil.mall@ringcentral.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://git@github.com:SushilMallRC/ringcentral-java.git'
                    developerConnection = 'scm:git:ssh://git@github.com:SushilMallRC/ringcentral-java.git'
                    url = 'https://github.com/SushilMallRC/ringcentral-java'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}
